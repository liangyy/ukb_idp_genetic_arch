if 'outdir' not in config:
    config['outdir'] = 'output'

rule all:
    input:
        '{outdir}/{idp_tag}_x_{gwas_tag}_x_{gwas_name}.ldsc_rg.log'.format(**config)

rule all_gwas:
    input:
        '{outdir}/{gwas_tag}.formatted_gwas.{gwas_name}.sumstats.gz'.format(**config)

rule format_focal:
    input:
        gwas = config['gwas_file_pattern'],
        snplist = config['ldscore']['snplist']
    params:
        n = config['gwas_file_params']['sample_size'],
        a1 = config['gwas_file_params']['effect_allele'],
        a2 = config['gwas_file_params']['non_effect_allele'],
        sign_stat = config['gwas_file_params']['signed_sumstats'],
        pval = config['gwas_file_params']['pvalue'],
        snp = config['gwas_file_params']['snp_col'],
        out_prefix = '{outdir}/{gwas_tag}.formatted_gwas.{gwas_name}'
    output:
        '{outdir}/{gwas_tag}.formatted_gwas.{gwas_name}.sumstats.gz'
    shell:
        'python {config[ldsc_repo]}/munge_sumstats.py \
           --sumstats {input.gwas} \
           --a1 {params.a1} \
           --a2 {params.a2} \
           --p {params.pval} \
           --snp {params.snp} \
           --N-col {params.n} \
           --out {params.out_prefix}'

rule gen_cor:
    input:
        gwas = '{outdir}/{gwas_tag}.formatted_gwas.{gwas_name}.sumstats.gz',
        ldscore = config['ldscore']['dir'],
        idplist = config['idp_list'],
        snpmeta = config['idp_snp_meta']
    params:
        idp_sample_size = config['idp_sample_size'],
        out_prefix = '{outdir}/{idp_tag}_x_{gwas_tag}_x_{gwas_name}.ldsc_rg'
    output:
        '{outdir}/{idp_tag}_x_{gwas_tag}_x_{gwas_name}.ldsc_rg.log'
    log:
        '{outdir}/{idp_tag}_x_{gwas_tag}_x_{gwas_name}.msg.log'
    shell:
        'python {config[ldsc_repo]}/ldsc.py \
           --rg {input.gwas},{input.idplist} \
           --rg-target-list \
           --ref-ld-chr {input.ldscore}/ \
           --w-ld-chr {input.ldscore}/ \
           --load-parquet \
           --snp-meta {input.snpmeta} \
           --sample-size {params.idp_sample_size} \
           --out {params.out_prefix} > {log} 2>&1'

